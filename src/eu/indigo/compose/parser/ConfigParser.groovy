package eu.indigo.compose.parser

import eu.indigo.compose.ProjectConfiguration
import eu.indigo.compose.ProjectConfigurationBuilder

import eu.indigo.JenkinsDefinitions

/**
 * Configuration Parser
 */
@groovy.transform.InheritConstructors
class ConfigParser extends JenkinsDefinitions implements Serializable {

    private static final long serialVersionUID = 0L

    // Constant literals for this Class
    String _repos = 'repos'

    private String LATEST = 'latest'
    private Integer DEFAULT_TIMEOUT = 600   // 600 seconds
    Map configToClass = [
        'docker-compose': 'DockerCompose'
    ]
    List supportedBuildTools = [
        'tox'
    ]

    ProjectConfiguration parse(yaml, env) {

        new ProjectConfigurationBuilder()
            .setNodeAgentAux(configToClass[(yaml.config.node_agent == null) ? 'docker-compose' : yaml.config.node_agent])
            .setConfig(getConfigSetting(yaml.config))
            .setStagesList(formatStages(getSQASetting(yaml['sqa-criteria'])))
            .setBuildNumber(env.BUILD_ID)
            .setEnvironment(parseEnvironment(yaml.environment))
            .setProjectName(parseProjectName(yaml.config))
            .setTimeout(yaml.timeout ?: DEFAULT_TIMEOUT)
            .build()
    }
    
    // from https://gist.github.com/robhruska/4612278
    Map merge(Map[] sources) {
        if (sources.length == 0) return [:]
        if (sources.length == 1) return sources[0]

        sources.inject([:]) { result, source ->
            source.each { k, v ->
                result[k] = result[k] instanceof Map ? merge(result[k], v) : v
            }
            result
        }
    }

    Map getDefaultValue(String setting) {
        steps.echo"** getDefaultValue(): ${setting}**"
        def result = [:]
        switch (setting) {
            case 'config':
                result = [
                    node_agent: 'docker-compose',
                    deploy_template: '.sqa/docker-compose.yml'
                ]
                break
            case 'config_repo':
                result = [
                    branch: 'master',
                    dockertag: 'latest'
                ]
                break
            case 'tox':
                result = [
                    container: 'tox',
                    tox: [
                        tox_file: 'tox.ini',
                        testenv: []
                    ]
                ]
                break
        }
        return result
    }

    Map getConfigSetting(Map config) {
        steps.echo "** getConfigSetting() **"
        def configBase = merge(getDefaultValue('config'), config)
        def configRepos = [
            project_repos: configBase['project_repos']
                .collectEntries { id, repo ->
                    [id, merge(getDefaultValue('config_repo'), repo)]
                }
        ]
        steps.echo 'configRepos:\n' + configRepos.toString()
        steps.echo 'configBase:\n' + configBase.toString()
        return merge(configBase, configRepos)
    }

    Map getSQASetting(Map criteria) {
        steps.echo "** getSQASetting() **"
        def sqaCriteria = criteria.each { criterion, data ->
            supportedBuildTools.each { tool ->
                def repoData = data[_repos].collectEntries { id, params ->
                    params.containsKey(tool) ? [id, merge(getDefaultValue(tool), params)] : null
                }
                data[_repos] = repoData
            }
        }
        return sqaCriteria
    }

    List formatStages(Map criteria) {
        def stagesList = []
        criteria.each { criterion, data ->
            def stageMap = [:]
            data[_repos].each { repo, params ->
                stageMap['stage'] = "${criterion} ${repo}"
                stageMap['repo'] = repo
                params.each { k, v ->
                    stageMap[k] = v
                }
            }
            stagesList.add(stageMap)
        }
        return stagesList
    }

    def parseEnvironment(def environment) {
        if (!environment) {
            return ''
        }

        return environment.collect { k, v -> "${k}=${v}"}
    }

    def parseProjectName(def config) {
        if (!config || !config["project_name"]) {
            return "composeci-project"
        }

        return config["project_name"]
    }

}
